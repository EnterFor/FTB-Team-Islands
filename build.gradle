plugins {
    id 'forgified-fabric-loom' version '0.6.79'
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

def ENV = System.getenv()
version = "${mod_version}-build.${ENV.GITHUB_RUN_NUMBER ?: 9999}"
archivesBaseName = project.archives_base_name
group = project.maven_group

loom {
    silentMojangMappingsLicense()

    mixinConfigs = []
    useFabricMixin = true

    dataGen {
        mod project.mod_id
    }

    launches {
        data {
            arg "--existing", file("src/main/resources").absolutePath
        }
    }
}

repositories {
    maven { url "https://www.cursemaven.com" }
    maven { url "https://maven.saps.dev" }
    maven { url "https://maven.latvian.dev" }
    maven { url "https://maven.shedaniel.me" }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"

    mappings minecraft.officialMojangMappings()
    forge "net.minecraftforge:forge:${project.minecraft_version}-${project.forge_version}"

    modCompile "dev.ftb.mods:ftb-library-forge:$ftb_mods_lib2"
    modCompile "dev.ftb.mods:ftb-chunks-forge:$ftb_mods_chunks"
    modCompile "dev.ftb.mods:ftb-teams-forge:$ftb_mods_teams"

    modCompile "me.shedaniel:architectury-forge:$arch_version"
}

processResources {
    inputs.property "version", project.version
    inputs.property "teams_version", ftb_mods_teams

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version, "team_version": ftb_mods_teams
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    def targetVersion = 8
    if (JavaVersion.current().isJava9Compatible()) {
        it.options.release = targetVersion
    }
}

java {
    // withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }

    manifest {
        attributes([
                "Specification-Title"     : project.mod_id,
                "Specification-Vendor"    : project.mod_author,
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : version,
                "Implementation-Vendor"   : project.mod_author,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }

    classifier "dev"
}

//if (ENV.CURSEFORGE_KEY) {
//    curseforge {
//        apiKey = ENV.CURSEFORGE_KEY
//        project {
//            id = project.curseforge_id
//            releaseType = "release"
//            addGameVersion "Forge"
//            addGameVersion "1.16.5"
//            mainArtifact(remapJar.archivePath)
//            changelog = getGitChangelog
//            changelogType = 'markdown'
//        }
//    }
//}

jar.finalizedBy('reobfJar')
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(jar) {
                artifactId = archivesBaseName + "-forge"
                classifier null
            }
        }
    }

    repositories {
        if (ENV.LOCAL_MAVEN) {
            maven {
                url = "file://${ENV.LOCAL_MAVEN}"
            }
        }

        if (ENV.SAPS_TOKEN) {
            maven {
                url "https://maven.saps.dev/minecraft"
                credentials {
                    username = "ftb"
                    password = "${ENV.SAPS_TOKEN}"
                }
            }
        }
    }
}
